plugins {
    id 'java'
    id "com.modrinth.minotaur" version "2.+"
    id("io.papermc.paperweight.userdev") version "1.5.5"
    id("xyz.jpenilla.run-paper") version "2.1.0" // Adds runServer and runMojangMappedServer tasks for testing
}

group = 'dev.sshear.way4homes'
version = project.mainVersion
archivesBaseName = "Waypoints-For-Homes_BUKKIT"

base {
    archivesName = "Waypoints-For-Homes_BUKKIT"
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.essentialsx.net/releases/" }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
    maven { url = "https://repo.bytecode.space/repository/maven-public" }
}

dependencies {
    compileOnly "net.essentialsx:EssentialsX:2.20.0"
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0'
    paperweight.paperDevBundle("1.20.1-R0.1-SNAPSHOT")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    archiveBaseName = "Waypoints-For-Homes_BUKKIT"
}

tasks {
    // Configure reobfJar to run when invoking the build task
    assemble {
        dependsOn(reobfJar)
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
    projectId = "waypoints-for-homes" // This can be the project ID or the slug. Either will work!
    versionType = "release" // This is the default -- can also be `beta` or `alpha`
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    gameVersions = ["1.20.1"] // Must be an array, even with only one version
    loaders = ["paper"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
    dependencies  {
        required.project "essentialsx"
    }
    syncBodyFrom = rootProject.file("README.md").text
}